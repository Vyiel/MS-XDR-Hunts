// Detect execution of PsExec but by Impacket package
// 
// Unlike the official PsExec, the one from Impacket creates random service names which is why it would be hard to create a rule/hunt for so.
// Although I noticed a similarity in the named pipe information, that whenever Impacket PsExec is executed, the PipeName contained RemCom followed by stdin or stdout and a random sequence of characters.
// 
// In this rule I searched first for Pipe Names and from that time zone, I scouted for Service Installed information, also getting the service name in the process.
// 
let DevEvents = 
DeviceEvents
| extend DevTime = bin(Timestamp, 1s)
| where ActionType == "NamedPipeEvent"
| where tolower(parse_json(AdditionalFields).PipeName) contains @"RemCom"
| where isnotempty(RemoteIP)
| extend InitiatorIP = RemoteIP
| summarize by DeviceId, InitiatorIP, DevTime;
// 
DeviceEvents
| extend DevTime2 = bin(Timestamp, 1s)
| where ActionType =~ @"ServiceInstalled"
| join kind=inner ( DevEvents ) on DeviceId
| where abs(datetime_diff('second', DevTime, DevTime2)) <= 3
| extend ServiceName = FileName
| project DevTime2, DeviceName, ServiceName, InitiatingProcessAccountName, InitiatorIP
