// Detect PsExec based Lateral Movement activities, an alternative edition includes IP Address
// 
// I have already created a rule for Detection of PsExec, which is great to detect PsExec based on Process Relationship and Disk Presence,
// but it didn't provide network information. This does exactly that, which helps in finding the initiator device for the lateral movement.
// 
// This detection fundamentally detecs named pipes with PsExeSvc on it, and it's remote IP connections for the initiator IP.
// 
let DevEvents = 
DeviceEvents
| extend DevTime = bin(Timestamp, 1s)
| where ActionType =~ @"ServiceInstalled"
| where tolower(FileName) contains @"psexesvc"
| project DevTime, DeviceId;
// 
DeviceEvents
| extend DevTime2 = bin(Timestamp, 1s)
| where ActionType == "NamedPipeEvent"
| join kind=inner ( DevEvents ) on DeviceId
| where abs(datetime_diff('second', DevTime, DevTime2)) <= 3
| where tolower(parse_json(AdditionalFields).PipeName) contains @"psexesvc"
| where isnotempty(RemoteIP)
| extend InitiatorIP = RemoteIP 
| summarize DetectionCount = count(), 
            FirstTimeDetected = min(Timestamp), 
            LastTimeDetected = max(Timestamp) 
  by DeviceName, RemoteIP
| order by FirstTimeDetected desc 
