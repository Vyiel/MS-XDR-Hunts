// Detecting credential theft from domain using LOLBIN ntdsutil.exe
//
// Similar to esentutl, this process also invokes vssvc.exe to invoke backup and storage of the NTDS.DIT file in the directory: C:\Windows\NTDS\NTDS.dit.
// This file when backed up by ntdsutil, can be exfiltrated for password extraction, but as this is the storage for DC, contains hashes for all domain users. 
// Note: To be able to crack the NTDS.DIT file, an adversary would also require the SYSTEM key which needs to be stolen as well, for example by esentutl or reg save.
//
// Upon execution, I didn't notice any device registry events but this time got two things. Along with that checked for the presence of the words create and full inside the command-line.
// From deviceEvents, I got ntdsutil.exe asking for further privileges following up with creation of vssvc.exe in the deviceProcessEvents as the underlying process involves Volume Shadow Copy service.
// So with those in mind and post adjusting for diluting precise check for time to the mili seconds, created the following query.
//
let deviceEventsCustom = materialize ( 
    DeviceEvents
    | extend deviceEventTime = bin(Timestamp, 1s)
    | where InitiatingProcessVersionInfoInternalFileName has "ntdsutil"
    | extend ifTokenChange = tostring(parse_json(AdditionalFields).TokenModificationProperties)
    | extend tokenChangeDesc = parse_json(ifTokenChange).tokenChangeDescription
    | where tokenChangeDesc has "Privileges were added to the process"
    | summarize by DeviceName, ActionType, InitiatingProcessFileName, InitiatingProcessCommandLine, deviceEventTime);
DeviceProcessEvents
| extend deviceProcessTime = bin(Timestamp, 1s)
| extend processEventTime = bin(Timestamp, 1s)
| join kind=inner ( deviceEventsCustom ) on DeviceName
| where abs(datetime_diff("second", deviceEventTime, processEventTime)) <= 2
| where ProcessVersionInfoInternalFileName has_any ("ntdsutil", "vssvc")
| where ProcessCommandLine matches regex @"(?i)c[^a-zA-Z]*r[^a-zA-Z]*e[^a-zA-Z]*a[^a-zA-Z]*t[^a-zA-Z]*e" 
or ProcessCommandLine matches regex @"(?i)f[^a-zA-Z]*u[^a-zA-Z]*l[^a-zA-Z]*l"
| project deviceProcessTime, DeviceName, FileName, InitiatingProcessFileName, InitiatingProcessParentFileName, ProcessCommandLine
| summarize DetectionCount = count(), 
            FirstTimeDetected = min(deviceProcessTime), 
            LastTimeDetected = max(deviceProcessTime) 
  by DeviceName, FileName, InitiatingProcessFileName, InitiatingProcessParentFileName, ProcessCommandLine
| project DeviceName, FileName, InitiatingProcessFileName, InitiatingProcessParentFileName, ProcessCommandLine, DetectionCount, FirstTimeDetected, LastTimeDetected
| order by FirstTimeDetected desc
