// Detection of phishing emails, that were delivered, has attachments, that we can hunt for.
// 
// The query is almost same as one of my previous query for detection of phishing emails delivered, with Allowed URL clicks, with the difference,
// that instead of URLClickEvents, I am hunting for Attachments.
// 
// Tablename: EmailAttachmentInfo. Similarly, you can see some telemetries are commented out, but can easily be tweeked based on actual environment data.
// 
let excludedDomains = dynamic(["google.com", "and more.com"]);
let TypesToWatch = dynamic(["pdf", "xlsx", "xlsm", "html", "docx", "docm", "zip", "gzip", "exe", "pptx", "pptm", "rtf", "dll", "xlam"]);
EmailEvents
    | extend MailTime = bin(Timestamp, 1s)
    | where EmailDirection == "Inbound"
    | where SenderFromDomain !in~ (excludedDomains)
    | extend DMARC = tostring(parse_json(AuthenticationDetails).DMARC)
    | extend PhishingConfidence = coalesce(tostring(parse_json(ConfidenceLevel).Phish), "None")
    | extend SpamConfidence = coalesce(tostring(parse_json(ConfidenceLevel).Spam), "None")
    // | where BulkComplaintLevel > 3
    | extend ComplaintLevels = case(BulkComplaintLevel <= 3, "Low", 
                        BulkComplaintLevel <= 7, "Medium", 
                        BulkComplaintLevel <= 9, "High",
                        "")
    // 
    | where DeliveryAction == "Delivered"
    | where PhishingConfidence !in ("Normal", "None")
    // | where ComplaintLevels !in ("Low")
    // | where DMARC == "fail"
    | join kind=inner EmailAttachmentInfo on NetworkMessageId
    | where FileType has_any (TypesToWatch)
    | project SenderFromAddress, SenderMailFromAddress, RecipientEmailAddress, Subject, EmailDirection, FileName, FileType, SHA256,
    ConfidenceLevel, PhishingConfidence, SpamConfidence, ThreatTypes, DeliveryAction, DeliveryLocation, EmailAction, EmailActionPolicy
