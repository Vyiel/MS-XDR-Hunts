// Detection of Credential Theft via LOLBIN: Vssadmin.
//
// Similar to esentutl, this process also invokes Volume Shadow Copy Services to invoke backup and storage of the NTDS.DIT file in the directory: C:\Windows\NTDS\NTDS.dit.
// This file when backed up by ntdsutil, can be exfiltrated for password extraction, but as this is the storage for DC, contains hashes for all domain users. 
// Note: To be able to crack the NTDS.DIT file, an adversary would also require the SYSTEM key which needs to be stolen as well, for example by esentutl or reg save.
//
// In this query, mainly deviceEvents and deviceProcessEvents table has been mapped as both of them leaves telemetries when the command is exuected in AD environment.
//
// Note: This command is also used by IT Administrators quite a lot, which is why the query needs to be modified in production based on what the baseline looks like.
// Note: The bellow query consists of 2 parts, which can be run individually or as an union. The deviceProcessEvents and deviceEvents both leaves telemetries where deviceProcessEvents concentrates a bit more on command lines and file names, whereas
// DeviceEvents concentrate a bit more on fundamental of token change request done by vssadmin when executed for this purpose. Combining both gives better visibility if the commands are obfuscated.
//
union ( 
    DeviceProcessEvents
    | extend processTime = bin(Timestamp, 1s)
    | where DeviceName !contains "-"
    | where ProcessVersionInfoInternalFileName contains @"vssadmin"
    | where ProcessCommandLine matches regex @"(?i)s[^a-zA-Z]*h[^a-zA-Z]*a[^a-zA-Z]*d[^a-zA-Z]*o[^a-zA-Z]*w" or
    ProcessCommandLine matches regex @"(?i)c[^a-zA-Z]*r[^a-zA-Z]*e[^a-zA-Z]*a[^a-zA-Z]*t[^a-zA-Z]*e"
    | where InitiatingProcessFileName has_any ("powershell", "cmd", "wscript")
),
(
    DeviceEvents
    | extend processTime = bin(Timestamp, 1s)
    | where InitiatingProcessVersionInfoInternalFileName contains @"vssadmin"
    | where InitiatingProcessCommandLine matches regex @"(?i)s[^a-zA-Z]*h[^a-zA-Z]*a[^a-zA-Z]*d[^a-zA-Z]*o[^a-zA-Z]*w" or
    InitiatingProcessCommandLine matches regex @"(?i)c[^a-zA-Z]*r[^a-zA-Z]*e[^a-zA-Z]*a[^a-zA-Z]*t[^a-zA-Z]*e"
)
| summarize DetectionCount = count(), 
            FirstTimeDetected = min(Timestamp), 
            LastTimeDetected = max(Timestamp) 
  by DeviceName, FileName, InitiatingProcessFileName, InitiatingProcessParentFileName, ProcessCommandLine, InitiatingProcessCommandLine
| project DeviceName, FileName, InitiatingProcessFileName, InitiatingProcessParentFileName, ProcessCommandLine, InitiatingProcessCommandLine, DetectionCount, FirstTimeDetected, LastTimeDetected
| order by FirstTimeDetected desc
